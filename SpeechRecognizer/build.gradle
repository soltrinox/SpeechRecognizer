apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

def grpcVersion = '0.15.0'
def supportVersion = '24.0.0'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        applicationId "com.crockettj.speechrecognizer.rpc"
        minSdkVersion 22
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    packagingOptions {
        pickFirst 'plugin.xml'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/services/io.grpc.ManagedChannelProvider'
    }

    lintOptions {
        disable 'InvalidPackage', 'HardcodedText'
        textReport true
        textOutput "stdout"
    }
}

protobuf {
    protoc {
        // This version must match the protobuf-java version that grpc transitively pulls in
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    //generatedFilesBaseDir = "$projectDir/generated" Ã¥
    generateProtoTasks {

        all().each {
            task ->
                task.builtins {
                    // Javanano is installed by default, but needs to be removed to use java
                    remove javanano
                    java {
                        // Options added to --java_out
                    }
                }

                task.plugins {
                    grpc {
                        // Options added to --grpc_out
                    }
                }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:multidex:1.0.1'

    testCompile 'junit:junit:4.12'

    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile "com.android.support:support-v4:${supportVersion}"

    compile 'com.google.code.findbugs:jsr305:3.0.0'

    // logging
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.github.tony19:logback-android-core:1.1.1-4'
    compile 'com.github.tony19:logback-android-classic:1.1.1-4'

    // required for Google Cloud Speech API
    //    compile 'com.google.guava:guava:19.0'
    compile 'javax.annotation:javax.annotation-api:1.2'
    
    // You need to build grpc-java to obtain these libraries below.
//    compile "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    compile "io.grpc:grpc-netty:${grpcVersion}"
//    compile "io.grpc:grpc-okhttp:${grpcVersion}" // only enable netty OR okhttp, not both. current has some netty stuff so do NOT include grpc-okhttp

    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-auth:${grpcVersion}"

    // google-auth-library-java??

    //compile 'com.google.android.gms:play-services-auth:9.2.0'
    compile 'com.google.auth:google-auth-library-oauth2-http:0.3.0'

    // Google OAUTH login
    compile 'com.google.oauth-client:google-oauth-client:1.22.0' exclude module: 'httpclient'
    compile 'com.google.http-client:google-http-client:1.22.0' exclude module: 'httpclient'
    compile 'com.google.http-client:google-http-client-android:1.22.0' exclude module: 'httpclient'
    compile 'com.google.http-client:google-http-client-jackson2:1.22.0' exclude module: 'httpclient'
    compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: '1.1.33.Fork17'

    protobuf files('src/main/libs/protos.jar')
}

idea {
    module {
        sourceDirs += file("${projectDir}/build/generated/source/proto/debug/grpc");
        sourceDirs += file("${projectDir}/build/generated/source/proto/debug/java");
    }
    // build/generated/source/proto/debug
}

